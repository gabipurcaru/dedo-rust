use std::str::FromStr;
use crate::types::{Value, Environment};

grammar<'a>(env: &'a mut Environment);

Num: f64 = r"[0-9]+(?:\.[0-9]+)?" => f64::from_str(<>).unwrap();

Ident: String = r"[a-zA-Z£$€_]+" => <>.into();

BasicTerm: Value = {
    "(" <BasicTerm> ")" => <>,
    "-" <BasicTerm> => Value::negate(<>), // TODO: preserve units
    <Num> <Ident> => Value::simple(<>),
    <i:Ident> <n:Num> => Value::simple(n, i),
    <Num> => Value::unitless(<>),
};

PowTerm: Value = {
    BasicTerm,
    <PowTerm> "**" <BasicTerm> => env.pow(<>),
    <PowTerm> "^" <BasicTerm> => env.pow(<>),
}

MulTerm: Value = {
    PowTerm,
    <MulTerm> "*" <PowTerm> => env.mul(<>),
    <MulTerm> "/" <PowTerm> => env.div(<>),
}

AddTerm: Value = {
    MulTerm,
    <AddTerm> "+" <MulTerm> => env.add(<>),
    <AddTerm> "-" <MulTerm> => env.sub(<>),
}

pub Term: Value = AddTerm;